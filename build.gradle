buildscript {

    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: '1.5.4.RELEASE'
        classpath group: 'org.springframework.build.gradle', name: 'propdeps-plugin', version: '0.0.7'

        classpath group: 'gradle.plugin.de.fuerstenau', name: 'BuildConfigPlugin', version: '1.1.4'
        classpath group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.6.0'
        classpath group: 'com.ofg', name: 'uptodate-gradle-plugin', version: '1.6.3'
    }

    repositories {
        jcenter()
        mavenCentral()

        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'

apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'

apply plugin: 'de.fuerstenau.buildconfig'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.ofg.uptodate'

group = 'com.kraluk.scf'
project.version = scmVersion.version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

scmVersion {
    tag {
        prefix = 'scf'
    }
}

repositories {
    mavenCentral()
    jcenter()

    maven { url 'http://labs.smsapi.com/maven/' }

    mavenLocal()
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

jar {
    baseName = 'scf'

    manifest {
        attributes("Specification-Title": "scf",
                "Specification-Version": scmVersion.version,
                "Implementation-Title": "scf",
                "Implementation-Version": scmVersion.version)
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

buildConfig {
    appName = project.name
    version = project.version

    packageName = 'com.kraluk.scf.server.util'
    clsName = 'Version'
}

bootRun {
    systemProperties = System.properties
}

uptodate {
    connectionTimeout = 10000
}

check.dependsOn 'uptodate'

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-rest'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-mail'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'

    compile group: 'org.springframework.boot', name: 'spring-boot-devtools'
    optional group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

    compile group: 'com.google.guava', name: 'guava', version: '22.0'
    compile group: 'pl.smsapi', name: 'smsapi-lib', version: '2.3'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.16'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

compileJava.dependsOn(processResources)

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}